plugins {
    kotlin("jvm")
    id("com.gradle.plugin-publish") version "0.16.0"
    id("java-gradle-plugin")
    id("java-library")
    id("maven-publish")
    id("groovy")
    id("klutter")
}

group = "dev.buijs.klutter"
version = dev.buijs.klutter.ProjectVersions.gradle

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

sourceSets {
    main {
        java {
            srcDirs("${projectDir.absolutePath}/src/main/kotlin")
        }
    }

    test {
        java {
            srcDirs("${projectDir.absolutePath}/src/test/kotlin")
        }
    }
}

publishing {
    repositories {
        maven {
            url = dev.buijs.klutter.Repository.endpoint
            credentials {
                username =  dev.buijs.klutter.Repository.username
                password =  dev.buijs.klutter.Repository.password
            }
        }
    }
}

pluginBundle {
    website = "https://buijs.dev/klutter/"
    vcsUrl = "https://github.com/buijs-dev/klutter"
    tags = listOf("klutter", "flutter", "kotlin", "multiplatform")
}

gradlePlugin {
    isAutomatedPublishing = false
    plugins {
        create("klutterGradlePlugin") {
            id = "dev.buijs.klutter"
            displayName = "Klutter plugin to generate boilerplate for connecting Flutter and Kotlin Multiplatform"
            description = "Klutter is a framework and tool set which uses Flutter to create the frontend and " +
                    "Kotlin Multiplatform for the backend. The connective layer is generated by the Klutter framework. " +
                    "This plugin contains all tasks needed to run a Klutter Project."
            implementationClass = "dev.buijs.klutter.gradle.KlutterGradlePlugin"
        }
    }
}

dependencies {
    // Project
    implementation(project(":lib:kore"))
    implementation(project(":lib:kradle"))

    // Kotlin: Required to check if Kotlin Multiplatform plugin is applied
    implementation("org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.10")

    // KSP Compiler plugin
    implementation("com.google.devtools.ksp:com.google.devtools.ksp.gradle.plugin:1.8.20-1.0.11")

    // Jackson XML/YAML parsing
    implementation("com.fasterxml.jackson.core:jackson-databind:2.14.2")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:2.14.2")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.14.2")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.2")

    // Logging
    implementation("org.slf4j:slf4j-api:2.0.7")
    implementation("io.github.microutils:kotlin-logging:3.0.5")

    // Test
    testImplementation(project(":lib-test"))

}

tasks.named<Test>("test") {
    useJUnitPlatform()
}

tasks.register("copyKlutterProperties", Copy::class) {
    from(project.rootProject.rootDir.resolve("lib-build/src/main/resources/publish.properties"))
    into(project.projectDir.resolve("src/main/resources"))
}